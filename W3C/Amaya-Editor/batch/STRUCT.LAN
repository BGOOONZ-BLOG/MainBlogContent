	{ This grammar defines the language S of Thot
	  The notation used here is defined in file META.LAN

	   ' '	: keyword
	   [ ]	: optional part
	   < >	: optional and repeatable part
	   /	: choice

	  V. Quint	March 1985	}


StructModel =	   'STRUCTURE' [ 'EXTENSION' ] ElemName ';'
		   'DEFPRES' PresentName ';'
		 [ 'ATTR' AttrList ]
		 [ 'CONST' ConstList ]
		 [ 'STRUCT' RuleList ]
		 [ 'EXTENS' ExtensList ]
		 [ 'UNITS' RuleList ]
		 [ 'EXPORT' ExpList ]
		 [ 'EXCEPT' ExceptList ]
		   'END' .

PresentName =	 NAME .

AttrList =	 Attribute < Attribute > .
Attribute =	 AttrName '=' AttrType ';' .
AttrType =	 'INTEGER' / 'TEXT' /
		 'REFERENCE' '(' TypeRef ')' /
		 ValueList .
TypeRef =	 'ANY' / [ FirstSec ] ElemName [ ExtStruct ] .
FirstSec =       'First' / 'Second' .
ValueList =	 AttrValue < ',' AttrValue > .

ConstList =	 Const < Const > .

Const =		 ConstName '=' ConstValue ';' .

ConstName =	 NAME .

ConstValue =	 STRING .

BasicType =	 'TEXT' / 'GRAPHICS' / 'SYMBOL' / 'PICTURE' / 'UNIT' /
		 'NATURE' .

RuleList =	 Rule ';' < Rule ';' > .
Rule =		 ElemName [ LocAttrList ] '=' DefWith .

ElemName =	 NAME .

LocAttrList =	 '(' 'ATTR' LocalAttr < ';' LocalAttr > ')' .
LocalAttr = 	 [ '!' ] AttrName [ '=' AttrType ] .

DefWith =	 Definition
		 [ '+' '(' InclList ')' ] [ '-' '(' ExclList ')' ]
		 [ 'WITH' FixedAttrList ] .

InclList =	 InclElem < ',' InclElem > .
InclElem =       ElemName / BasicType .
ExclList =	 ExclElem < ',' ExclElem > .
ExclElem =       ElemName / BasicType .

Definition =	 BasicType [ LocAttrList ]  / Constr / Element .
Element =	 ElemName [ ExtOrDef ] .
ExtOrDef =	 'EXTERN' / 'INCLUDED' / [ LocAttrList ] '=' Definition .

Constr =	 'LIST' [ '[' min '.' '.' max ']' ] 'OF' '(' DefWith ')' /
		 'BEGIN' OptDefList 'END' /
		 'AGGREGATE' OptDefList 'END' /
		 'CASE' 'OF' DefList 'END' /
		 'REFERENCE' '(' TypeRef ')' /
		 'CONSTANT' ConstName /
		 'PAIR' / 'ANY' .

OptDefList =	 OptDefWith ';' < OptDefWith ';' > .

OptDefWith =	 [ '?' ] DefWith .

DefList =	 DefWith ';' < DefWith ';' > .

min =		 Integer / '*' .

max =		 Integer / '*' .

Integer =	 NUMBER .

FixedAttrList =	 FixedAttr < ',' FixedAttr > .
FixedAttr =	 AttrName [ FixModValue ] .

FixModValue =	 ['?'] '=' FixedValue .
FixedValue =	 [ '-' ] NumValue / StrValue / AttrValue .

AttrName =	 NAME .

AttrValue =	 NAME .

NumValue =	 NUMBER .

StrValue =	 STRING .

ExtStruct =	 '(' ElemName ')' .

ExpList =	 Export < ',' Export > ';' .

Export =	 ElemName [ 'WITH' Content ] .

Content =	 'Nothing' / ElemName [ ExtStruct ] .

ExceptList =	 Except ';' < Except ';' > .
Except =	 ExceptType ':' ExValList .
ExceptType =	 BasicType / [ 'EXTERN' ] [ FirstSec ] ElemName / AttrName / ConstName .
ExValList =	 ExceptVal < ',' ExceptVal > .
ExceptVal =	 'NoCut' / 'CanCut' / 'NoCreate' / 'NoReplicate' /
                 'NoHMove' / 'NoVMove' / 'NoMove' /
		 'NoHResize' / 'NoVResize' / 'NoResize' / 'MoveResize' /
		 'NewWidth' / 'NewHeight' / 'NewPercentWidth' /
		 'NewHPos' / 'NewVPos' /
                 'Invisible' / 'Hidden' /
	         'PageBreak' / 'PageBreakAllowed' / 'PageBreakPlace' /
	         'PageBreakRepetition' / 'PageBreakRepBefore' /
                 'NoPaginate' / 'NoSpellCheck' /
		 'ActiveRef' /  'NoSelect' /
                 'HighlightChildren' / 'ExtendedSelection' /
                 'SelectParent' / 'ClickableSurface' /
		 'ParagraphBreak' / 'IsBreak' / 'ListItemBreak' /
		 'ReturnCreateNL' / 'ReturnCreateWithin' / 'NoBreakByReturn' /
                 'ImportLine' / 'ImportParagraph' /
		 'GraphCreation' / 'IsDraw' / 'IsGroup' / 'EmptyGraphic' /
		 'IsTable' / 'IsRow' / 'IsColHead' / 'IsCell' / 'IsCaption' /
		 'ColRef' / 'ColSpan' / 'RowSpan' /
		 'CssBackground' / 'CssClass' / 'CssId' / 'CssPseudoClass' /
		 'NoShowBox' / 'SetWindowBackground' /
		 'Shadow' / 'EventAttr' / 'SpacePreserve' /
		 'IsPlaceholder' / 'StartCounter' / 'SetCounter' /
		 'MarkupPreserve' / 'NotAnElementNode' / 'CheckAllChars' /
                 'IsImg' / 'IsMap' / 'DuplicateAttr' / 'PseudoParag' /
                 'GiveName' / 'NewRoot' / 'GiveTypes' / 'IsMarker' / 'IsGhost' /
		 'ColColRef' / 'UsePaintServer' / 'UseMarkers' / ExceptNum .
ExceptNum =	 NUMBER .

ExtensList =     Extens ';' < Extens ';' > .
Extens =	 RootOrElem [ LocAttrList ]
		 [ '+' '(' InclList ')' ] [ '-' '(' ExclList ')' ]
		 [ 'WITH' FixedAttrList ] .
RootOrElem =     'Root' / ElemName .

END
