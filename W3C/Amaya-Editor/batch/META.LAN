	{ This grammar defines the meta-language used to describe the Thot
	languages S, P, and T.
	This grammar is itself written in that meta-language.

	V. Quint	June 1984	}

Grammar =	Rule < Rule > 'END' .		{ pointy brackets < and >
						  indicate a repetition }
Rule =		Ident '=' RightHandSide '.' .	{ a period indicates the end
						  of a rule }
RightHandSide =	RightTerminal / RightIntermed .	{ a slash separates options }

RightTerminal =	'NAME' / 'STRING' / 'NUMBER' .	{ right hand side of a
						  terminal rule }
RightIntermed =	Option < '/' Option > .		{ right hand side of an
						  intermediate rule }
Option =	ElRepeatOpt < ElRepeatOpt > .	{ square brackets [ and ]
						  indicate an optional part }
ElRepeatOpt =	Element / '[' Element < Element > ']' /
		'<' Element < Element > '>'  .

Element =	Ident / KeyWord .

Ident =		NAME .

KeyWord =	STRING .

END
